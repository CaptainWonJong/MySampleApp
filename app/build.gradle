apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId "com.ctwj.mysampleapp"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
            resConfigs("xxhdpi")
            aaptOptions.cruncherEnabled = false
        }
        prod {
            dimension "default"
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    dataBinding {
        enabled = true
        enabledForTests = true
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // androidx library (option + Enter 로 최신버전 항상 확인)
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-rc01"
    implementation "androidx.paging:paging-runtime:2.1.0"
    implementation "android.arch.paging:rxjava2:2.1.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.palette:palette:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation 'androidx.media:media:1.1.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.browser:browser:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.viewpager2:viewpager2:1.0.0-rc01'
    implementation("androidx.fragment:fragment-testing:1.2.0-beta01", {
        exclude group: 'androidx.test', module: 'core'
    })

    // Firebase
    implementation "com.google.firebase:firebase-messaging:$firebaseVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // GSON
    implementation "com.google.code.gson:gson:$gsonVersion"

    // EventBus
    implementation "org.greenrobot:eventbus:$eventBusVersion"

    // Koin
    implementation "org.koin:koin-core:$koinVersion"
    implementation "org.koin:koin-androidx-scope:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"

    // Square(OkHttp, Retrofit)
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"

    // Reactive X
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxBindingVersion"

    // leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'

    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}
